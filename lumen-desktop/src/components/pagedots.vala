public class PageDots : Gtk.Box {

    public signal void page_clicked(int page);

    private Gtk.Button[] dots;
    private int active = 0;

    public PageDots(int page_count) {
        Object(orientation: Gtk.Orientation.HORIZONTAL, spacing: 10);
        add_css_class("page-dots");
        set_halign(Gtk.Align.CENTER);

        set_count(page_count);
    }

    // Reused across an app-list reload so the same PageDots widget can take a
    // new page count in place, rather than being swapped out.
    public void set_count(int page_count) {
        Gtk.Widget? c;
        while ((c = get_first_child()) != null) c.unparent();

        dots = new Gtk.Button[page_count];
        active = 0;
        for (int i = 0; i < page_count; i++) {
            var dot = new Gtk.Button.with_label((i + 1).to_string());
            dot.add_css_class("page-dot");
            // strip default button chrome; .page-dot CSS provides background
            dot.add_css_class("flat");
            int page = i;
            dot.clicked.connect(() => page_clicked(page));
            dots[i] = dot;
            append(dot);
        }
        if (page_count > 0) dots[0].add_css_class("active");
    }

    public void set_active(int page) {
        if (page < 0 || page >= dots.length) return;
        if (active < dots.length) dots[active].remove_css_class("active");
        dots[page].add_css_class("active");
        active = page;
    }
}
